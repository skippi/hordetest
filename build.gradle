plugins {
    id "java-library"
    id "com.diffplug.spotless" version "6.10.0"
    id "io.papermc.paperweight.userdev" version "1.3.8"
    id "xyz.jpenilla.run-paper" version "1.0.6" // Adds runServer and runMojangMappedServer tasks for testing
    id "net.minecrell.plugin-yml.bukkit" version "0.5.2" // Generates plugin.yml
}

repositories {
    mavenCentral()
    maven { url "https://papermc.io/repo/repository/maven-public/" }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
    mavenLocal()
}

dependencies {
    paperweightDevelopmentBundle("io.papermc.paper:dev-bundle:1.19.2-R0.1-SNAPSHOT")
    compileOnly "com.comphenix.protocol:ProtocolLib:5.0.0-SNAPSHOT"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

spotless {
  java {
    importOrder()
    removeUnusedImports()
    googleJavaFormat("1.15.0")
  }
}

bukkit {
    load = "STARTUP"
    main = "io.github.skippi.hordetest.HordeTestPlugin"
    apiVersion = "1.19"
    depend = ["ProtocolLib"]
    website = "https://github.com/skippi/hordetest"
    authors = ["skippi"]
    commands {
        stage {}
    }
}

tasks.named("assemble") {
    dependsOn(reobfJar)
}

tasks.named("test") {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs += ["-Xlint:deprecation"]
}
